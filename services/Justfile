set dotenv-load := true
set dotenv-path := "../.env"

export IMAGE := env_var_or_default("IMAGE", "ghcr.io/jambrains/service-sdk:latest")

default:
	@echo "Build an example with 'just build [<name>]' or all examples with 'just build'"
	@echo "Available examples:"
	@ls *.c | perl -ne 's/\.c$//; print'

# Build a specific example by name (without .c extension)
build *name: output
	#!/usr/bin/env bash
	set -e

	if [ -z "$DOCKER" ]; then
		echo "DOCKER environment variable is not set. Please run either 'just docker' or 'just podman' in the root folder first."
		exit 1
	fi

	# If name is empty, then we build all examples
	if [ -z "{{name}}" ]; then
		for n in *.c; do
			name=$(basename $n .c)
			just build $name
		done
	else
		for n in {{name}}; do
			$DOCKER run --rm \
				-v{{justfile_directory()}}:/app \
				-w /app/output \
				{{IMAGE}} single-file /app/${n}.c ${n}
		done
	fi

output:
	@mkdir -p output

clean:
	rm -rf output/

watch:
	find . ../sdk | entr -c "just"
